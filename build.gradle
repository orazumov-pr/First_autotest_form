plugins {
    id 'java-library'
    id 'com.github.ben-manes.versions' version '0.53.0'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencyUpdates {
    rejectVersionIf {
        ['alpha', 'beta', 'rc', 'm', 'snapshot', 'preview', 'eap', 'dev'].any { keyword ->
            it.candidate.version.toLowerCase().contains(keyword)
        }
    }
    // Опционально: проверка только стабильных версий
    checkForGradleUpdate = true
    outputFormatter = 'json'
}

def junitVersion = '6.0.2'

dependencies {
    testImplementation(
            "org.junit.jupiter:junit-jupiter-api:${junitVersion}",
            "org.junit.jupiter:junit-jupiter-params:${junitVersion}",
            "org.junit.platform:junit-platform-launcher:${junitVersion}",
            "org.junit.jupiter:junit-jupiter-engine:${junitVersion}",
            'org.slf4j:slf4j-simple:2.0.17')
    implementation('com.codeborne:selenide:7.14.0')
}


tasks.withType(Test).configureEach {
    systemProperties(System.getProperties())
    useJUnitPlatform()

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat = "short"
        }
    }
}